dnl
dnl autoconf input for wmCoinCoin
dnl rcsid=$id$
dnl ChangeLog: 
dnl   $Log: configure.in,v $
dnl   Revision 1.11  2002/03/05 21:04:28  pouaite
dnl   bugfixes suite à l'upgrade de dlfp [et retour au comportement à l'ancienne du clic sur les horloges pour les moules ronchonnes]
dnl
dnl   Revision 1.10  2002/02/24 22:13:56  pouaite
dnl   modifs pour la v2.3.5 (selection, scrollcoin, plopification, bugfixes)
dnl
dnl   Revision 1.9  2002/01/30 21:03:45  pouaite
dnl   correction du bug du au signe des char, et d'un petit bug dans les reference d'horloges
dnl
dnl   Revision 1.8  2002/01/20 20:53:22  pouaite
dnl   bugfix configure.in && http_win.c pour cygwin + 2-3 petis trucs
dnl
dnl   Revision 1.7  2002/01/13 20:38:31  pouaite
dnl   fix du configure.in qui detecte cygwin a tors et à travers avec les v. recentes de autoconf
dnl
dnl   Revision 1.6  2002/01/10 09:03:07  pouaite
dnl   integration du patch de glandium (requetes http/1.1 avec header 'If-Modified-Since' --> coincoin plus gentil avec dacode)
dnl
dnl   Revision 1.5  2002/01/03 22:15:31  pouaite
dnl   patch top moumoute de glandium pour la gestion du wiki
dnl
dnl   Revision 1.4  2001/12/17 18:11:07  pouaite
dnl   attention chérie ça va releaser
dnl
dnl   Revision 1.3  2001/12/16 01:43:33  pouaite
dnl   filtrage des posts, meilleure gestion des posts multiples
dnl
dnl   Revision 1.2  2001/12/02 18:32:05  pouaite
dnl   update suite à la modif sur http.c et a la generation automatique de src/default*.h
dnl

AC_INIT(src/wmcoincoin.c)

AM_INIT_AUTOMAKE(wmcoincoin, 2.3.6dev)
AM_CONFIG_HEADER(src/config.h)

dnl Checks for host/os name
dnl =======================
AC_CANONICAL_HOST
COIN_ARCH="${host_cpu}-${host_vendor}-${host_os}"
AC_SUBST(COIN_ARCH)
case $host_os in
  *cygwin* ) CYGWIN=yes;;
         * ) CYGWIN=no;;
esac

dnl Checks for programs.
dnl ===================
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl don't want the -g flag
CFLAGS=`echo "x $CFLAGS"|sed 's/^x //; s/\(^\| \)-g\($\| \)/ /g'`

dnl additional compile flags when using gcc
if test x$GCC = xyes; then
  CFLAGS="$CFLAGS -Wall -W -fsigned-char";
else
  echo "Vous n'utilisez pas gcc .. attention coincoin peut avoir quelque bugs si les char ne sont pas signés"
fi

AC_PATH_X
AC_PATH_XTRA
echo x_cflags=$X_CFLAGS libs=$LIBS pre_lib=$X_PRE_LIBS x_libs=$X_LIBS x_extra=$X_EXTRA_LIBS
AC_CHECK_LIB(Xpm, XpmCreatePixmapFromData,,
  AC_MSG_ERROR(The XPM library is required but wasn't found.),
  $X_LIBS -lX11 -lXext)

LIBS="${X_PRE_LIBS} ${X_LIBS} -lX11 -lXext ${LIBS} ${X_EXTRA_LIBS}"
CFLAGS="$CFLAGS $X_CFLAGS"
  
case "$host" in
    *-linux-*)
          echo "vous utilisez linux, c'est bien !"
          ;;
    *-solaris*)
          echo "oh oh !! wmcc sur solaris marche a peu pres, bon courage !"
          ;;
    *bsd*)
          echo "bsd detected!"
	  ;;
    *-cygwin*)
	  echo "cygwin detected !"
     	  AC_CHECK_LIB(wsock32,select)
	  dnl correct a bug in cygwin: incorrect regex included in -lm
	  LIBS="`echo ${LIBS} | sed -e 's/-lm//'`"
	  LIBS="${LIBS} /lib/libregex.a -lm"        
	  ;;
    *-darwin*)
          echo "darwin detected !"
          ;;
    *)
         echo "vous compilez sur $host , c'est original"
         ;;
esac

dnl Checks for library functions.
dnl ============================
AC_FUNC_VPRINTF
AC_CHECK_HEADERS(string.h stdarg.h unistd.h sys/time.h utime.h sys/utime.h)
AC_CHECK_HEADERS(sys/select.h sys/utsname.h pwd.h signal.h windows.h)
AC_HEADER_TIME
AC_CHECK_FUNCS(strdup strstr strcasecmp strncasecmp)
AC_CHECK_FUNCS(strerror snprintf vsnprintf select signal)
AC_CHECK_FUNCS(uname gethostname)

AC_C_CONST

AC_OUTPUT(Makefile src/Makefile doc/Makefile) 


